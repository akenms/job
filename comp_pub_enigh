clear
cap clear
cap log close
scalar drop _all
set mem 400m
set more off
#delimit;

*Este programa debe ser utilizado con el Software Stata 
versión 8 o posterior. 

Todas las bases de datos deben estar en formato *.dta

En este programa se utilizan las siguientes bases del MCS-ENIGH 2010, 2012 y 2014
y ENIGH 2016 renombrándolas de la siguiente forma:

Base de población: Poblacion10.dta
                   Poblacion12.dta
				   Poblacion14.dta
				   Poblacion16.dta
				   
Base de concentrado: Concentrado10.dta
                     Concentrado12.dta
					 Concentrado14.dta
					 Concentrado16.dta

*Este programa calcula el indicador de gasto inferior al costo de la canasta 
básica alimentaria para los años 2010, 2012, 2014 y 2016.;

gl data_10="C:\Indicadores complementarios 2010-2016\ENIGH\2010";
gl data_12="C:\Indicadores complementarios 2010-2016\ENIGH\2012";
gl data_14="C:\Indicadores complementarios 2010-2016\ENIGH\2014";
gl data_16="C:\Indicadores complementarios 2010-2016\ENIGH\2016";

gl bases="C:\Indicadores complementarios 2010-2016\temp";
gl base_final="C:\Indicadores complementarios 2010-2016\temp\bases_finales";
gl log="C:\Indicadores complementarios 2010-2016\log";

log using "$log\indicomp_gasto_ENIGH2010-2016.txt", text replace;

****************************************************************************
*PARTE 1. Construcción del indicador de gasto en alimentación a nivel hogar 
****************************************************************************;
use "$data_10\poblacion10.dta", clear;
save "$bases\poblacion10.dta", replace;

use "$data_12\poblacion12.dta", clear;
save "$bases\poblacion12.dta", replace;

use "$data_14\poblacion14.dta", clear;
save "$bases\poblacion14.dta", replace;

use "$data_16\poblacion16.dta", clear;
save "$bases\poblacion16.dta", replace;

use "$data_10\concentrado10.dta", clear;

gen str folio= folioviv + foliohog;
sort folio;
gen decena=real(substr(folio,3,1));

*Generar la canasta de alimentos;

gen can_urb =.;
replace can_urb=978.29 if (decena>=0 & decena<=1);
replace can_urb=986.47 if (decena>=2 & decena<=4);
replace can_urb=996.02 if (decena>=5 & decena<=7);
replace can_urb=1002.56 if (decena>=8 & decena<=9);


gen can_rur =.;
replace can_rur=683.72 if (decena>=0 & decena<=1);
replace can_rur=691.05 if (decena>=2 & decena<=4);
replace can_rur=699.65 if (decena>=5 & decena<=7);
replace can_rur=704.69 if (decena>=8 & decena<=9);


sort folio;
keep folioviv foliohog folio factor decena tam_loc can* alimentos;
save "$bases\Gasto_alimentos10.dta", replace;

use "$data_12\concentrado12.dta", clear;

gen str folio= folioviv + foliohog;
sort folio;
gen decena=real(substr(folio,3,1));

rename factor_hog factor;

*Generar la canasta de alimentos;

gen can_urb =.;
replace can_urb=1125.42 if (decena>=0 & decena<=1);
replace can_urb=1144.81 if (decena>=2 & decena<=4);
replace can_urb=1147.94 if (decena>=5 & decena<=7);
replace can_urb=1148.65 if (decena>=8 & decena<=9);


gen can_rur =.;
replace can_rur=800.26 if (decena>=0 & decena<=1);
replace can_rur=818.50 if (decena>=2 & decena<=4);
replace can_rur=820.33 if (decena>=5 & decena<=7);
replace can_rur=816.83 if (decena>=8 & decena<=9);

destring tam_loc, replace;
sort folio;
keep folioviv foliohog folio factor decena tam_loc can* alimentos;
save "$bases\Gasto_alimentos12.dta", replace;

use "$data_14\concentrado14.dta", clear;

gen str folio= folioviv + foliohog;
sort folio;
gen decena=real(substr(folio,8,1));

rename factor_hog factor;

*Generar la canasta de alimentos;

gen can_urb =.;
replace can_urb=1242.61 if (decena>=0 & decena<=1);
replace can_urb=1257.07 if (decena>=2 & decena<=4);
replace can_urb=1264.92 if (decena>=5 & decena<=7);
replace can_urb=1272.91 if (decena>=8 & decena<=9);


gen can_rur =.;
replace can_rur=868.25 if (decena>=0 & decena<=1);
replace can_rur=881.39 if (decena>=2 & decena<=4);
replace can_rur=887.26 if (decena>=5 & decena<=7);
replace can_rur=895.84 if (decena>=8 & decena<=9);

destring tam_loc, replace;
sort folio;
keep folioviv foliohog folio factor decena tam_loc can* alimentos;
save "$bases\Gasto_alimentos14.dta", replace;

use "$data_16\concentrado16.dta", clear;

gen str folio= folioviv + foliohog;
sort folio;
gen decena=real(substr(folio,8,1));

*Generar la canasta de alimentos;

gen can_urb =.;
replace can_urb=1310.94 if (decena>=1 & decena<=2);
replace can_urb=1339.39 if (decena>=3 & decena<=5);
replace can_urb=1346.46 if (decena>=6 & decena<=8);
replace can_urb=1355.72 if (decena==9 | decena==0);


gen can_rur =.;
replace can_rur=933.20 if (decena>=1 & decena<=2);
replace can_rur=959.59 if (decena>=3 & decena<=5);
replace can_rur=963.17 if (decena>=6 & decena<=8);
replace can_rur=969.53 if (decena==9 | decena==0);

destring tam_loc, replace;
sort folio;
keep folioviv foliohog folio factor decena tam_loc can* alimentos;
save "$bases\Gasto_alimentos16.dta", replace;


foreach x in 10 12 14 16{;

*************************
Escalas de equivalencia
*************************;

use "$bases\poblacion`x'.dta", clear;
*Población objeto: no se incluye a huéspedes ni trabajadores domésticos;

drop if parentesco>="400" & parentesco <"500";
drop if parentesco>="700";

*Total de integrantes del hogar;
gen ind=1;
egen tot_ind=sum(ind), by (folioviv foliohog);

gen n_05=.;
replace n_05=1 if edad>=0 & edad<=5;
recode n_05 (.=0) if edad!=.;

gen n_6_12=0;
replace n_6_12=1 if edad>=6 & edad<=12;
recode n_6_12 (.=0) if edad!=.;

gen n_13_18=0;
replace n_13_18=1 if edad>=13 & edad<=18;
recode n_13_18 (.=0) if edad!=.;

gen n_19=0;
replace n_19=1 if edad>=19 & edad!=.;
recode n_05 (.=0) if edad!=.;

gen tamhogesc=n_05*.7031;
replace tamhogesc=n_6_12*.7382 if n_6_12==1;
replace tamhogesc=n_13_18*.7057 if n_13_18==1;
replace tamhogesc=n_19*.9945 if n_19==1 ;
replace tamhogesc=1 if tot_ind==1;
replace tamhogesc=1 if parentesco=="101";

collapse (sum)  tamhogesc, by(folioviv foliohog);

gen str folio= folioviv + foliohog;

sort folio;

save "$bases\tamhogesc`x'.dta", replace;
*******************************

*******************************;

use "$bases\Gasto_alimentos`x'.dta", clear;

*Unir el tamaño de hogares ajustado a escalas de adulto equivalente;
merge folio using "$bases\tamhogesc`x'.dta";
tab _merge;
drop _merge;
sort folio;
*Año;
gen año=20`x';

*Considerando A001-A247 (tabaco y alcohol);
gen double alimpc= alimentos/(3*tamhogesc);

*Generación rural-urbano;
gen rururb=1 if tam_loc==4;
replace rururb=0 if tam_loc<=3;
label define rururb 1 "Rural" 0 "Urbano";
label value rururb rururb;

*Al comparar con la información del catálogo, se observa que la 
información se levantó en diez decenas, correspondientes a:

Decena |	Periodo de levantamiento 					Periodo de referencia de mes pasado 
1      |    11 - 20 de agosto                                   julio
2      |    21 - 30 de agosto                                   julio
3      |    31 de agosto - 9 de septiembre                      agosto   
4      |    10 - 19 de septiembre                               agosto  
5      |    20 - 29 de septiembre                               agosto  
6      |    30 de septiembre - 9 de octubre                     septiembre
7      |    10 - 19 de octubre                                  septiembre
8      |    20 - 29 de octubre                                  septiembre
9      |    30 de octubre - 8 de noviembre                      octubre
0      |    9 - 18 de noviembre                                 octubre


*Gasto en alimentación
Porcentaje de hogares con un gasto en alimentación ajustado
por economías de escala y escalas de adulto equivalente, inferior al costo de la canasta
básica alimentaria;

gen  poblp1=0  if alimpc!=.;
replace poblp1=1 if alimpc<can_urb & rururb==0;
replace poblp1=1 if alimpc<can_rur & rururb==1;
 
order folioviv folioviv foliohog factor año tam_loc alimentos folio decena 
can_urb can_rur tamhogesc alimpc rururb poblp1;

save "$bases\Indcomp_alim`x'_enigh.dta", replace;


};
*Unir los diferentes años de los indicadores;
use "$bases\Indcomp_alim10_enigh.dta",clear;
append using "$bases\Indcomp_alim12_enigh.dta";
append using "$bases\Indcomp_alim14_enigh.dta";
append using "$bases\Indcomp_alim16_enigh.dta";

label var año "Año";
label var tam_loc "Tamaño de localidad";
label var alimentos "Alimentos";
label var can_urb "Canasta urbana";
label var can_rur "Canasta rural";
label var tamhogesc "Tamaño del hogar escalado";
label var alimpc "Gasto en alimentación escalado";
label var rururb "Tipo de localidad";
label var poblp1 "Gasto en alimentación escalado";

label define pob 0 "Superior a la canasta básica alimentaria" 
                      1 "Inferior a la canasta básica alimentaria";
label value poblp1 pob;

sort folio;

save "$base_final\Indicadores_complementarios_enigh.dta",replace;

***********************************************
*PARTE 2. Tabulado del indicador a nivel hogar
***********************************************;

di in green "INDICADORES COMPLEMENTARIOS: ALIMENTACIÓN";

di in red "Gasto en alimentación";
di in yellow "Porcentaje de hogares con un gasto en alimentación ajustado por economías de escala y escalas de adulto equivalente, inferior al costo 
de la canasta básica alimentaria";
****************************************************************************************************************************************************;
tabstat poblp1 [w=factor],by(año);

log close;
